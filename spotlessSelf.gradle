buildscript { repositories { jcenter() } }

// applied by SelfTest
plugins {
	id 'com.diffplug.gradle.spotless'
	id 'java'
}

repositories { jcenter() }
spotless {
	def noInternalDepsClosure = {
		if (it.contains('import org.gradle.internal.') &&
		!it.contains('def noInternalDepsClosure')) {
			throw new AssertionError("Accidental internal import")
		}
	}
	java {
		target fileTree('.') {
			include '**/*.java'
			exclude '_ext/*/build/**'
		}
		custom 'noInternalDeps', noInternalDepsClosure
		bumpThisNumberIfACustomStepChanges(1)
		licenseHeaderFile 'spotless.license'
		importOrderFile   'spotless.importorder'
		eclipseFormatFile 'spotless.eclipseformat.xml'
		trimTrailingWhitespace()
		removeUnusedImports()
	}
	groovy {
		target fileTree('.') {
			include '**/*.gradle'
			exclude '_ext/**'
		}
		paddedCell()
		custom 'noInternalDeps', noInternalDepsClosure
		bumpThisNumberIfACustomStepChanges(3)
		greclipse().configFile('spotless.eclipseformat.xml', 'spotless.groovyformat.prefs')
	}
	freshmark {
		target '**/*.md'
		propertiesFile('gradle.properties')
		properties {
			it.put('yes', ':+1:')
			it.put('no', ':white_large_square:')
		}
	}
	format 'misc', {
		target '**/*.md', '**/*.gitignore'
		indentWithTabs()
		trimTrailingWhitespace()
		endWithNewline()
	}
}
