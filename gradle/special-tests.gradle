apply plugin: 'com.adarshr.test-logger'

// See com.diffplug.spotless.tag package for available JUnit 5 @Tag annotations
def special = [
	'Black',
	'Buf',
	'Clang',
	'gofmt',
	'Npm',
	'Shfmt'
]

boolean isCiServer = providers.environmentVariable("CI").isPresent()
tasks.withType(Test).configureEach {
	if (isCiServer) {
		retry {
			maxRetries = 2
			maxFailures = 10
		}

		// There are only 2 cores in each GitHub Action Runner, we use all of them here.
		maxParallelForks = Runtime.getRuntime().availableProcessors()
	} else {
		// https://docs.gradle.org/8.4/userguide/performance.html#execute_tests_in_parallel
		maxParallelForks = Runtime.runtime.availableProcessors().intdiv(2) ?: 1
	}
}
tasks.named('test').configure {
	useJUnitPlatform {
		excludeTags special as String[]
	}
}
special.forEach { tag ->
	tasks.register("test${tag}", Test) {
		useJUnitPlatform { includeTags tag }
	}
}
