apply plugin: 'com.diffplug.spotless'
spotless {
	if (project != rootProject) {
		// the rootProject doesn't have any java
		java {
			ratchetFrom 'origin/main'
			bumpThisNumberIfACustomStepChanges(1)
			// `setMutators` will discard default mutators
			// https://sonarsource.atlassian.net/browse/RSPEC-6212
			cleanthat().version("2.9").sourceCompatibility(project.sourceCompatibility.toString()).clearMutators()
					.addMutator('RSPEC-6212')
			licenseHeaderFile rootProject.file('gradle/spotless.license')
			importOrderFile   rootProject.file('gradle/spotless.importorder')
			eclipse().configFile rootProject.file('gradle/spotless.eclipseformat.xml')
			trimTrailingWhitespace()
			removeUnusedImports()
			formatAnnotations()
			forbidWildcardImports()
			forbidRegex('ForbidGradleInternal', 'import org\\.gradle\\.api\\.internal\\.(.*)', "Don't use Gradle's internal API")
		}
	}
	groovyGradle {
		target '*.gradle'
		greclipse().configFile rootProject.files('gradle/spotless.eclipseformat.xml', 'gradle/spotless.groovyformat.prefs')
	}
}
