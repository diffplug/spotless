apply plugin: 'com.diffplug.spotless'
spotless {
	if (project != rootProject) {
		// the rootProject doesn't have any java
		java {
			bumpThisNumberIfACustomStepChanges(1)
			eclipse().configFile rootProject.file('gradle/spotless.eclipseformat.xml')
			// expandWildcardImports() todo: use after release
			forbidWildcardImports()
			forbidRegex('ForbidGradleInternal', 'import org\\.gradle\\.api\\.internal\\.(.*)', "Don't use Gradle's internal API")
			formatAnnotations()
			importOrderFile   rootProject.file('gradle/spotless.importorder')
			licenseHeaderFile rootProject.file('gradle/spotless.license')
			ratchetFrom 'origin/main'
			removeUnusedImports()
			trimTrailingWhitespace()
		}
	}
	groovyGradle {
		target '*.gradle', 'gradle/*.gradle'
		greclipse().configFile rootProject.files('gradle/spotless.eclipseformat.xml', 'gradle/spotless.groovyformat.prefs')
	}
	format 'dotfiles', {
		target '.gitignore', '.gitattributes', '.editorconfig'
		leadingTabsToSpaces(2)
		trimTrailingWhitespace()
		endWithNewline()
	}
}
