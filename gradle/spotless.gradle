apply plugin: 'com.diffplug.spotless'
spotless {
	def noInternalDepsClosure = {
		String text = it
		/*
		 * No good way to get around using this import:
		 * https://github.com/gradle/gradle/issues/3191
		 */
		String regex = "import org\\.gradle\\.api\\.internal\\.(?!plugins\\.DslObject)(?!project\\.ProjectInternal)"
		if ((text.contains('import org.gradle.internal.') || text.find(regex)) &&
				!text.contains('def noInternalDepsClosure')) {
			throw new AssertionError("Accidental internal import")
		}
	}
	if (project != rootProject) {
		// the rootProject doesn't have any java
		java {
			ratchetFrom 'origin/main'
			bumpThisNumberIfACustomStepChanges(1)
			cleanthat().version("2.4").sourceCompatibility("11").clearMutators().addMutators(['RSPEC-6212'])
			licenseHeaderFile rootProject.file('gradle/spotless.license')
			importOrderFile   rootProject.file('gradle/spotless.importorder')
			eclipse().configFile rootProject.file('gradle/spotless.eclipseformat.xml')
			trimTrailingWhitespace()
			removeUnusedImports()
			// TODO: formatAnnotations()
			custom 'noInternalDeps', noInternalDepsClosure
		}
	}
	groovyGradle {
		target '*.gradle'
		greclipse().configFile rootProject.files('gradle/spotless.eclipseformat.xml', 'gradle/spotless.groovyformat.prefs')
	}
}
