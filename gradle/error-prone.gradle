import static java.lang.System.getenv

apply plugin: 'net.ltgt.errorprone'

tasks.withType(JavaCompile).configureEach {
	options.errorprone {
		disableAllWarnings = true // https://github.com/diffplug/spotless/issues/2745 https://github.com/google/error-prone/issues/5365
		disable(
				// consider fix, or reasoning.
				'AnnotateFormatMethod', // We don`t want to use ErrorProne's annotations.
				'DoNotCallSuggester', // We don`t want to use ErrorProne's annotations.
				'FunctionalInterfaceMethodChanged',
				'ImmutableEnumChecker', // We don`t want to use ErrorProne's annotations.
				'InlineMeSuggester', // We don`t want to use ErrorProne's annotations.
				'JavaxInjectOnAbstractMethod',
				'OverridesJavaxInjectableMethod',
				'ReturnValueIgnored', // We don`t want to use ErrorProne's annotations.
				)
		error(
				'SelfAssignment',
				'StringJoin',
				'UnnecessarilyFullyQualified',
				'UnnecessaryLambda',
				)
		// bug: this only happens when the file is dirty.
		// might be an up2date (caching) issue, as file is currently in corrupt state.
		// ForbidGradleInternal(import
		excludedPaths.set(
				'.*/GradleIntegrationHarness.java|'+
				'.*/SelfTest.java'
				)
		if (!getenv().containsKey('CI') && getenv('IN_PLACE')?.toBoolean()) {
			errorproneArgs.addAll(
					'-XepPatchLocation:IN_PLACE',
					'-XepPatchChecks:' +
					'MissingOverride'
					)
		}
	}
}

dependencies {
	errorprone('com.google.errorprone:error_prone_core:2.42.0')
}
