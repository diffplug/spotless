import static java.lang.System.getenv

apply plugin: 'net.ltgt.errorprone'

dependencies {
	errorprone('com.google.errorprone:error_prone_core:2.42.0')
	errorprone('tech.picnic.error-prone-support:error-prone-contrib:0.25.0')
	constraints {
		errorprone('com.google.guava:guava') {
			version {
				require('33.4.8-jre')
			}
			because('Older versions use deprecated methods in sun.misc.Unsafe')
			// https://github.com/junit-team/junit-framework/pull/5039#discussion_r2414490581
		}
	}
}

tasks.withType(JavaCompile).configureEach {
	options.errorprone {
		disableAllChecks = true // consider removal to avoid error prone errorÂ´s, following convention over configuration.
		//error('ConstantNaming')
		if (!getenv().containsKey('CI') && getenv('IN_PLACE')?.toBoolean()) {
			errorproneArgs.addAll(
					'-XepPatchLocation:IN_PLACE',
					'-XepPatchChecks:ConstantNaming'
					)
		}
	}
}
