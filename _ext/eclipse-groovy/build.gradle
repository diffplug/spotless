ext {
	developers = [
		fvgh: [ name: 'Frank Vennemeyer', email: 'frankgh@zoho.com' ],
	]

	p2Repository = "https://dist.springsource.org/release/GRECLIPSE/${VER_GRECLIPSE}/e${VER_ECLIPSE}"

	p2Dependencies = [
		'org.codehaus.groovy.eclipse.refactoring':'+', // GroovyFormatter and related

		// The following lists does not reflect the complete transitive required packages, but
		// the once used during code formatting
		'org.codehaus.groovy':'+', // Groovy compiler patches supporting use within GrEclipse and Groovy itself
		'org.codehaus.groovy.eclipse.core':'+', // Groovy core classes (provides central logging used by formatter)
		'org.eclipse.jdt.core':"${VAR_GRECLIPSE_JDT_PATCH}", // Patches org.eclipse.jdt.core classes supporting use within GrEclipse (provides AST generator)
		'org.eclipse.jdt.groovy.core':'+' // Extends org.eclipse.jdt.core for Groovy
	]

	internalJars = [
		//Jars included by org.codehaus.groovy
		//////////////////////////////////////////////////////////////////////////
		// Use Groovy compiler compatible with GrEclipse instead of localGroovy
		"**/groovy-${VER_GROOVY}",
		"**/groovy-parser2",
		// Patches/Overrides some of the Groovy compiler classes
		'**/groovy-eclipse',
		// Provides logging capabilities for groovy-eclipse
		'**/eclipse-trace',
		//Jars included by org.eclipse.jdt.groovy.core
		//////////////////////////////////////////////////////////////////////////
		//Non locking class loader used by groovy compiler
		'**/nlcl'
	]

}

apply from: rootProject.file('_ext/gradle/update-lockfile.gradle')
apply from: rootProject.file('_ext/gradle/p2-fat-jar-setup.gradle')
apply from: rootProject.file('gradle/java-publish.gradle')

dependencies {
	implementation "com.diffplug.spotless:spotless-eclipse-base:${VER_SPOTLESS_ECLIPSE_BASE}"
	// Provides text partitioners for formatters
	implementation ("org.eclipse.platform:org.eclipse.jface.text:${VER_ECLIPSE_JFACE}") {
		exclude group: 'org.eclipse.platform', module: 'org.eclipse.swt'
	}
	testImplementation("org.slf4j:slf4j-simple:${VER_SLF4J}")
}

//////////
// Test //
//////////
sourceSets {
	// Use JAR file with all resources for Eclipse-Groovy integration-tests
	test.runtimeClasspath = jar.outputs.files + sourceSets.test.output + sourceSets.test.compileClasspath
}
