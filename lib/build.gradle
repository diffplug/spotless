plugins {
	id 'java-library'
	id 'io.github.davidburstrom.version-compatibility'
}
ext.artifactId = project.artifactIdLib
version = rootProject.spotlessChangelog.versionNext
apply from: rootProject.file('gradle/java-setup.gradle')
apply from: rootProject.file('gradle/java-publish.gradle')

def NEEDS_GLUE = [
	'sortPom',
	'palantirJavaFormat',
	'ktfmt',
	'ktlint',
	'flexmark',
	'diktat',
	'scalafmt'
]
for (glue in NEEDS_GLUE) {
	sourceSets.register(glue) {
		compileClasspath += sourceSets.main.output
		runtimeClasspath += sourceSets.main.output
		java {}
	}
}

versionCompatibility {
	adapters {
		namespaces.register('KtLint') {
			versions = [
				'0.31.0',
				'0.32.0',
				'0.34.2',
				'0.45.2',
				'0.46.0',
				'0.47.0',
			]
			targetSourceSetName = 'ktlint'
		}
	}
}

dependencies {
	compileOnly 'org.slf4j:slf4j-api:2.0.0'
	// zero runtime reqs is a hard requirements for spotless-lib
	// if you need a dep, put it in lib-extra
	testImplementation "org.junit.jupiter:junit-jupiter:$VER_JUNIT"
	testImplementation "org.assertj:assertj-core:$VER_ASSERTJ"
	testImplementation "com.diffplug.durian:durian-testlib:$VER_DURIAN"

	// used for pom sorting
	sortPomCompileOnly 'com.github.ekryd.sortpom:sortpom-sorter:3.0.0'
	sortPomCompileOnly 'org.slf4j:slf4j-api:2.0.0'

	palantirJavaFormatCompileOnly 'com.palantir.javaformat:palantir-java-format:1.1.0'

	String VER_KTFMT = '0.41'
	ktfmtCompileOnly "com.facebook:ktfmt:$VER_KTFMT"
	String VER_KTLINT_GOOGLE_JAVA_FORMAT = '1.7' // for JDK 8 compatibility
	ktfmtCompileOnly("com.google.googlejavaformat:google-java-format") {
		version {
			strictly VER_KTLINT_GOOGLE_JAVA_FORMAT
		}
	}

	String VER_KTLINT='0.46.1'
	ktlintCompileOnly "com.pinterest:ktlint:$VER_KTLINT"
	ktlintCompileOnly "com.pinterest.ktlint:ktlint-core:$VER_KTLINT"
	ktlintCompileOnly "com.pinterest.ktlint:ktlint-ruleset-experimental:$VER_KTLINT"
	ktlintCompileOnly "com.pinterest.ktlint:ktlint-ruleset-standard:$VER_KTLINT"

	compatKtLint0Dot31Dot0CompileOnly 'org.jetbrains.kotlin:kotlin-stdlib-jdk8:1.5.20'
	compatKtLint0Dot31Dot0CompileOnly 'com.github.shyiko.ktlint:ktlint-core:0.31.0'
	compatKtLint0Dot31Dot0CompileOnly 'com.github.shyiko.ktlint:ktlint-ruleset-experimental:0.31.0'
	compatKtLint0Dot31Dot0CompileOnly 'com.github.shyiko.ktlint:ktlint-ruleset-standard:0.31.0'
	compatKtLint0Dot32Dot0CompileOnly 'org.jetbrains.kotlin:kotlin-stdlib-jdk8:1.5.20'
	compatKtLint0Dot32Dot0CompileOnly 'com.pinterest.ktlint:ktlint-core:0.32.0'
	compatKtLint0Dot32Dot0CompileOnly 'com.pinterest.ktlint:ktlint-ruleset-experimental:0.32.0'
	compatKtLint0Dot32Dot0CompileOnly 'com.pinterest.ktlint:ktlint-ruleset-standard:0.32.0'
	compatKtLint0Dot34Dot2CompileOnly 'org.jetbrains.kotlin:kotlin-stdlib-jdk8:1.5.20'
	compatKtLint0Dot34Dot2CompileOnly 'com.pinterest.ktlint:ktlint-core:0.34.2'
	compatKtLint0Dot34Dot2CompileOnly 'com.pinterest.ktlint:ktlint-ruleset-experimental:0.34.2'
	compatKtLint0Dot34Dot2CompileOnly 'com.pinterest.ktlint:ktlint-ruleset-standard:0.34.2'
	compatKtLint0Dot45Dot2CompileOnly 'com.pinterest.ktlint:ktlint-core:0.45.2'
	compatKtLint0Dot45Dot2CompileOnly 'com.pinterest.ktlint:ktlint-ruleset-experimental:0.45.2'
	compatKtLint0Dot45Dot2CompileOnly 'com.pinterest.ktlint:ktlint-ruleset-standard:0.45.2'
	compatKtLint0Dot46Dot0CompileOnly 'com.pinterest.ktlint:ktlint-core:0.46.0'
	compatKtLint0Dot46Dot0CompileOnly 'com.pinterest.ktlint:ktlint-ruleset-experimental:0.46.0'
	compatKtLint0Dot46Dot0CompileOnly 'com.pinterest.ktlint:ktlint-ruleset-standard:0.46.0'
	compatKtLint0Dot47Dot0CompileOnly 'com.pinterest.ktlint:ktlint-core:0.47.0'
	compatKtLint0Dot47Dot0CompileOnly 'com.pinterest.ktlint:ktlint-ruleset-experimental:0.47.0'
	compatKtLint0Dot47Dot0CompileOnly 'com.pinterest.ktlint:ktlint-ruleset-standard:0.47.0'

	String VER_SCALAFMT="3.6.1"
	scalafmtCompileOnly "org.scalameta:scalafmt-core_2.13:$VER_SCALAFMT"

	String VER_DIKTAT = "1.2.3"
	diktatCompileOnly "org.cqfn.diktat:diktat-rules:$VER_DIKTAT"

	// used for markdown formatting
	flexmarkCompileOnly 'com.vladsch.flexmark:flexmark-all:0.62.2'
}

// we'll hold the core lib to a high standard
spotbugs { reportLevel = 'low' } // low|medium|high (low = sensitive to even minor mistakes)

test { useJUnitPlatform() }

jar {
	for (glue in NEEDS_GLUE) {
		from sourceSets.getByName(glue).output.classesDirs
	}
}
