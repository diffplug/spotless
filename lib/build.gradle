plugins {
	id 'java-library'
}
ext.artifactId = project.artifactIdLib
version = rootProject.spotlessChangelog.versionNext
apply from: rootProject.file('gradle/java-setup.gradle')
apply from: rootProject.file('gradle/java-publish.gradle')

def NEEDS_GLUE = [
	'sortPom',
	'palantirJavaFormat',
	'ktlint',
	'flexmark'
]
for (glue in NEEDS_GLUE) {
	sourceSets.register(glue) {
		compileClasspath += sourceSets.main.output
		runtimeClasspath += sourceSets.main.output
		java {}
	}
}

dependencies {
	// zero runtime reqs is a hard requirements for spotless-lib
	// if you need a dep, put it in lib-extra
	testImplementation "org.junit.jupiter:junit-jupiter:$VER_JUNIT"
	testImplementation "org.assertj:assertj-core:$VER_ASSERTJ"
	testImplementation "com.diffplug.durian:durian-testlib:$VER_DURIAN"

	// used for pom sorting
	sortPomCompileOnly 'com.github.ekryd.sortpom:sortpom-sorter:3.0.0'

	palantirJavaFormatCompileOnly 'com.palantir.javaformat:palantir-java-format:2.16.0'

	String VER_KTLINT='0.43.2'
	ktlintCompileOnly "com.pinterest:ktlint:$VER_KTLINT"
	ktlintCompileOnly "com.pinterest.ktlint:ktlint-core:$VER_KTLINT"
	ktlintCompileOnly "com.pinterest.ktlint:ktlint-ruleset-standard:$VER_KTLINT"

	// used for markdown formatting
	flexmarkCompileOnly 'com.vladsch.flexmark:flexmark-all:0.62.2'
}

// we'll hold the core lib to a high standard
spotbugs { reportLevel = 'low' } // low|medium|high (low = sensitive to even minor mistakes)

test { useJUnitPlatform() }

jar {
	for (glue in NEEDS_GLUE) {
		from sourceSets.getByName(glue).output.classesDirs
	}
}
