import de.benediktritter.maven.plugin.development.task.GenerateHelpMojoSourcesTask
import de.benediktritter.maven.plugin.development.task.GenerateMavenPluginDescriptorTask

plugins {
	// https://www.benediktritter.de/maven-plugin-development/#release-history
	id 'de.benediktritter.maven-plugin-development' version '0.4.1'
}

apply from: rootProject.file('gradle/changelog.gradle')
ext.artifactId = project.artifactIdMaven
version = spotlessChangelog.versionNext

apply from: rootProject.file("gradle/java-setup.gradle")
apply from: rootProject.file('gradle/spotless-freshmark.gradle')

mavenPlugin {
	name = 'Spotless Maven Plugin'
	artifactId = project.artifactIdMaven
	description = project.description
}

tasks.withType(GenerateMavenPluginDescriptorTask).configureEach {
	notCompatibleWithConfigurationCache('https://github.com/britter/maven-plugin-development/issues/8')
}
tasks.withType(GenerateHelpMojoSourcesTask).configureEach {
	notCompatibleWithConfigurationCache('https://github.com/britter/maven-plugin-development/issues/8')
}

String VER_MAVEN_API = '3.0'
String VER_ECLIPSE_AETHER = '1.1.0'
String VER_PLEXUS_RESOURCES = '1.2.0'
dependencies {
	implementation project(':lib')
	implementation project(':lib-extra')

	compileOnly "org.apache.maven:maven-plugin-api:${VER_MAVEN_API}"
	compileOnly "org.apache.maven.plugin-tools:maven-plugin-annotations:${VER_MAVEN_API}"
	compileOnly "org.apache.maven:maven-core:${VER_MAVEN_API}"
	compileOnly "org.eclipse.aether:aether-api:${VER_ECLIPSE_AETHER}"

	implementation "com.diffplug.durian:durian-core:${VER_DURIAN}"
	implementation "com.diffplug.durian:durian-collect:${VER_DURIAN}"
	implementation("org.codehaus.plexus:plexus-resources:${VER_PLEXUS_RESOURCES}")
	implementation "org.eclipse.jgit:org.eclipse.jgit:${VER_JGIT}"
	implementation 'org.sonatype.plexus:plexus-build-api:0.0.7'

	testImplementation project(":testlib")
	testImplementation "org.junit.jupiter:junit-jupiter:${VER_JUNIT}"
	testImplementation "org.assertj:assertj-core:${VER_ASSERTJ}"
	testImplementation "org.mockito:mockito-core:${VER_MOCKITO}"
	testImplementation "com.diffplug.durian:durian-io:${VER_DURIAN}"
	testImplementation 'com.github.spullara.mustache.java:compiler:0.9.10'
	testImplementation "org.apache.maven:maven-plugin-api:${VER_MAVEN_API}"
	testImplementation "org.eclipse.aether:aether-api:${VER_ECLIPSE_AETHER}"
	testImplementation "org.codehaus.plexus:plexus-resources:${VER_PLEXUS_RESOURCES}"
	testImplementation "org.apache.maven:maven-core:${VER_MAVEN_API}"
}

apply from: rootProject.file('gradle/special-tests.gradle')
tasks.withType(Test).configureEach {
	systemProperty 'spotlessMavenPluginVersion', project.version
	dependsOn 'publishToMavenLocal'
	dependsOn ':lib:publishToMavenLocal'
	dependsOn ':lib-extra:publishToMavenLocal'
}

apply from: rootProject.file("gradle/java-publish.gradle")
