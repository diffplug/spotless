apply from: rootProject.file('gradle/changelog.gradle')
ext.artifactId = project.artifactIdGradle
version = spotlessChangelog.versionNext
apply from: rootProject.file('gradle/java-setup.gradle')
apply from: rootProject.file('gradle/spotless-freshmark.gradle')

apply plugin: 'java-library'
apply plugin: 'com.gradle.plugin-publish'
apply plugin: 'java-gradle-plugin'

dependencies {
	if (version.endsWith('-SNAPSHOT')) {
		api project(':lib')
		api project(':lib-extra')
	} else {
		api "com.diffplug.spotless:spotless-lib:${rootProject.spotlessChangelog.versionLast}"
		api "com.diffplug.spotless:spotless-lib-extra:${rootProject.spotlessChangelog.versionLast}"
	}
	implementation "com.diffplug.durian:durian-core:${VER_DURIAN}"
	implementation "com.diffplug.durian:durian-io:${VER_DURIAN}"
	implementation "com.diffplug.durian:durian-collect:${VER_DURIAN}"
	implementation "org.eclipse.jgit:org.eclipse.jgit:${VER_JGIT}"

	testImplementation project(':testlib')
	testImplementation "junit:junit:${VER_JUNIT}"
	testImplementation "org.assertj:assertj-core:${VER_ASSERTJ}"
	testImplementation "com.diffplug.durian:durian-testlib:${VER_DURIAN}"
}

test { testLogging.showStandardStreams = true }

test {
	useJUnit {
		excludeCategories 'com.diffplug.spotless.category.NpmTest'
	}
}

task npmTest(type: Test) {
	useJUnit {
		includeCategories 'com.diffplug.spotless.category.NpmTest'
	}
}

// make it easy for eclipse to run against latest build
tasks.eclipse.dependsOn(pluginUnderTestMetadata)

//////////////////////////
// GRADLE PLUGIN PORTAL //
//////////////////////////
gradlePlugin {
	plugins {
		spotlessPlugin {
			id = 'com.diffplug.spotless'
			implementationClass = 'com.diffplug.gradle.spotless.SpotlessPlugin'
			displayName = 'Spotless formatting plugin'
			description = project.description
		}
		spotlessPluginLegacy {
			id = 'com.diffplug.gradle.spotless'
			implementationClass = 'com.diffplug.gradle.spotless.SpotlessPluginRedirect'
			displayName = 'Spotless formatting plugin (legacy)'
			description = project.description
		}
	}
}
if (version.endsWith('-SNAPSHOT')) {
	publishPlugins.enabled = false
} else {
	pluginBundle {
		// These settings are set for the whole plugin bundle
		website = "https://github.com/diffplug/spotless"
		vcsUrl = "https://github.com/diffplug/spotless"
		tags = [
			'format',
			'style',
			'license',
			'header',
			'google-java-format',
			'eclipse',
			'ktlint',
			'ktfmt',
			'tsfmt',
			'prettier',
			'scalafmt',
			'scalafix'
		]
		plugins {
			spotlessPlugin {
				id = 'com.diffplug.spotless'
			}
			spotlessPluginLegacy {
				id = 'com.diffplug.gradle.spotless'
			}
		}
		mavenCoordinates {
			groupId = project.group
			artifactId = project.artifactIdGradle
			version = project.version
		}
	}
}

// have to apply java-publish after setting up the pluginBundle
apply from: rootProject.file('gradle/java-publish.gradle')
