plugins {
	// https://github.com/diffplug/goomph/blob/main/CHANGES.md
	id 'com.diffplug.eclipse.resourcefilters' version '3.28.0'
	// https://plugins.gradle.org/plugin/com.gradle.plugin-publish
	id 'com.gradle.plugin-publish' version '0.13.0' apply false
	// https://github.com/bintray/gradle-bintray-plugin/releases
	id 'com.jfrog.bintray' version '1.8.5' apply false
	// https://github.com/mnlipp/jdrupes-mdoclet/releases
	id 'org.jdrupes.mdoclet' version '1.0.10' apply false
	// https://github.com/spotbugs/spotbugs-gradle-plugin/releases
	id "com.github.spotbugs" version "4.6.2" apply false
	//https://github.com/diffplug/goomph
	id "com.diffplug.p2.asmaven" version "3.28.0" apply false
	// https://github.com/diffplug/spotless-changelog
	id "com.diffplug.spotless-changelog" version "2.0.0" apply false
}

apply from: rootProject.file('gradle/changelog.gradle')
allprojects {
	apply from: rootProject.file('gradle/spotless.gradle')
}
apply from: rootProject.file('gradle/spotless-freshmark.gradle')

spotless {
	groovyGradle {
		target '*.gradle', 'gradle/*.gradle'
	}
	format 'dotfiles', {
		target '.gitignore', '.gitattributes', '.editorconfig'
		indentWithSpaces(2)
		trimTrailingWhitespace()
		endWithNewline()
	}
}

// root eclipse project
apply plugin: 'com.diffplug.eclipse.resourcefilters'
eclipseResourceFilters {
	exclude().folders().name('.git')
	exclude().folders().name('build').recursive()

	exclude().folders().name('lib')
	exclude().folders().name('lib-extra')
	exclude().folders().name('plugin-gradle')
	exclude().folders().name('plugin-maven')
	exclude().folders().name('testlib')
}

static Class<?> spotBugsTaskType() {
	return com.github.spotbugs.snom.SpotBugsTask
}
